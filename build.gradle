buildscript {
    ext.kotlin_version = '1.3.10'
    repositories {
        jcenter()
        google()
        maven { url 'https://oss.jfrog.org/libs-snapshot' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://oss.jfrog.org/libs-snapshot' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'

// Use `com.nerdery.umbrella.darkskyapikey` in gradle.properties or as a command line
// parameter or `DARK_SKY_API_KEY` as a System Environment variable to provide dark
// sky api key to this project
ext.darkSkyApiKey = project.hasProperty('com.nerdery.umbrella.darkskyapikey') ?
        project.getProperty('com.nerdery.umbrella.darkskyapikey') :
        System.getenv('DARK_SKY_API_KEY') ?: ''

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    signingConfigs {
        umbrella {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        applicationId "com.nerdery.umbrella"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'

        buildConfigField "String", "API_KEY", "\"$project.darkSkyApiKey\""

        signingConfig signingConfigs.umbrella
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    JETPACK_VERSION = "1.0.0"
    RETROFIT_VERSION = "2.6.1"
    OKHTTP_VERSION = "4.0.1"
    FACEBOOK_VERSION = "1.5.1"
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"

    implementation "io.reactivex.rxjava2:rxjava:2.2.7"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

    implementation "com.jakewharton.byteunits:byteunits:0.9.1"
    implementation "com.jakewharton.threetenabp:threetenabp:1.1.1"
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.0-alpha05'
    debugImplementation "com.facebook.stetho:stetho:$FACEBOOK_VERSION"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$FACEBOOK_VERSION"
    debugImplementation "com.facebook.stetho:stetho-timber:$FACEBOOK_VERSION@aar"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
